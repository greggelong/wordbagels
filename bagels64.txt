10 REM BAGELS
20 REM C64 BASIC
21 REM VARIABLE NAMES ARE ONLY 2CHARS
22 REM BE CARFUL NAMING
30 REM*GET RAND SECRET NUMBERS****
100 S1 = INT(RND(0)*10)
110 S2 = INT(RND(0)*10)
120 IF S2 = S1 THEN 110:REM CHECK
130 S3 = INT(RND(0)*10)
140 IF S3 = S1 OR S3 = S2 THEN 130
150 PRINT"{clr}";
151 POKE 53281,0
152 PRINT"{green}";
153 PRINT"{red}nmnnnmnnnmnmnmnmnmmnnnnm"
154 PRINT"{green}";
160 PRINT"I AM THINKING OF A NUMBER"
170 PRINT"YOU HAVE 10 GUESSES"
175 PRINT"I WILL GIVE YOU CLUES:"
180 PRINT"PICO - 1 DIGIT BUT WRONG ORDER"
182 PRINT"FERMI - 1 DIGIT RIGHT ORDER
183 PRINT"BAGELS - NO DIGITS"
184 PRINT"{red}nmnnnmnnnmnmnmnmnmmnnnnm"
189 PRINT"{green}";
190 REM  PRINT S1,S2,S3
200 REM*GET INPUT*******
210 FOR I = 1 TO 10
250 INPUT"YOUR GUESS";G$
260 PRINT"GUESS:",I, G$
300 REM*CHECK**********
305 H = 0 : REM FOR BAGELS
310 T1=VAL(MID$(G$,1,1))
320 T2=VAL(MID$(G$,2,1))
330 T3=VAL(MID$(G$,3,1))
335 REM** TEST FOR WIN********
340 IF T1 = S1 AND T2 = S2 AND T3 = S3 THEN 1000
345 REM TEST FOR FERMI PICO
350 IF T1 = S1 THEN PRINT"FERMI":H=H+1
360 IF T1 = S2 OR T1 = S3 THEN PRINT "PICO": H = H+1
370 IF T2 = S2 THEN PRINT"FERMI":H=H+1
380 IF T2 = S1 OR T2 = S3 THEN PRINT "PICO": H = H+ 1
390 IF T3 = S3 THEN PRINT"FERMI":H=H+1
400 IF T3 = S2 OR T3 = S1 THEN PRINT "PICO" : H=H+1
410 REM TEST FOR BAGLES
420 IF H = 0 THEN PRINT "BAGELS"
499 NEXT I
900 PRINT "TOO MANY GUESSES!!"
910 PRINT "THE ANSWER WAS"
920 PRINT S1;S2;S3
930 END
1000 PRINT "YOU WIN!!!!" 
